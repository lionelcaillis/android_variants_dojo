apply plugin: 'com.android.application'
apply plugin: 'bkJenkinsBuild'
apply from: '../jacoco.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.backelite.androidvariantsdojo"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

// VARIANTS UPDATE #1: Transform misused build types to flavors thanks to flavors dimensions
// Warning: Don't forget to move src folders if necessary
    flavorDimensions "bank", "type"
    productFlavors {
        sg {
            dimension "bank"
            applicationId "mobi.societegenerale.mobile"
            versionName "3"
        }
        cdn {
            dimension "bank"
            applicationId "com.ocito.cdn.activity"
            versionName "1"
        }

        pro_hf {
            dimension "type"
            applicationIdSuffix "lapplipro_hf"
        }
        ent_hf {
            dimension "type"
            applicationIdSuffix "sogecashnet_hf"
        }
        propri {
            dimension "type"
            applicationIdSuffix "creditdunord"
        }
        ent {
            dimension "type"
            applicationIdSuffix "creditdunordent"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

// VARIANTS UPDATE #2: Since versionNameSuffix could only be used un buildTypes (unlike applicationIdSuffic),
// we need to complete version name manually
        applicationVariants.all { variant ->
            if (variant.productFlavors.find { it.name == 'pro_hf' } != null) {
                variant.mergedFlavor.versionName += '.1'
            } else {
                variant.mergedFlavor.versionName += '.0'
            }
        }
    }

// VARIANTS UPDATE #3: Once all variants are composed, we can set some as ignored (ie: not buildable)
    variantFilter { variant ->
        def dim = variant.flavors.collectEntries {
            [(it.productFlavor.dimension): it.productFlavor.name]
        }

        if ((dim.bank == 'cdn' && dim.type != 'propri' && dim.type != 'ent')
                || (dim.bank == 'sg' && dim.type != 'pro_hf' && dim.type != 'ent_hf')) {
            variant.ignore = true
        }
    }
}

bkJenkinsBuild {
    projectName "androidvariantsdojo-appli-android"
}

sonarqube {

    properties {
        properties["sonar.sources"] = ["src/main", "src/cdn", "src/sg"]
        // Leave default "sonar.binaries" property
        //properties["sonar.binaries"] = file("$buildDir/intermediates/classes/debug")
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
}
